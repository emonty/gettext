dnl Process this file with autoconf to produce a configure script.
AC_PREREQ(2.52)
AC_INIT
AC_CONFIG_SRCDIR(vasprintf.c)
AM_INIT_AUTOMAKE(libasprintf, 1.0)
AM_CONFIG_HEADER(config.h)

dnl Checks for programs.
AC_PROG_CC
AC_PROG_CXX
AC_PROG_INSTALL

dnl Check for host type.
AC_CANONICAL_HOST

dnl Checks for compiler output filename suffixes.
AC_OBJEXT
AC_EXEEXT

dnl Check for build configuration.
AC_LIBTOOL_WIN32_DLL
AC_PROG_LIBTOOL

dnl Checks for libraries.

dnl Checks for header files.

dnl Checks for typedefs, structures, and compiler characteristics.
AC_C_CONST
bh_C_SIGNED
AC_TYPE_SIZE_T
gt_TYPE_LONGLONG
gt_TYPE_LONGDOUBLE
gt_TYPE_WCHAR_T
AC_CHECK_TYPES(ptrdiff_t)
gt_TYPE_INTMAX_T

dnl Checks for library functions.
AC_FUNC_ALLOCA
AC_CHECK_FUNCS([snprintf vasprintf])

dnl Check for C++ libraries needed to avoid link errors when creating
dnl libasprintf.
LIBCXX=
cat > conftest.cc <<EOF
#ifdef __SUNPRO_C
SunPro C
#endif
#ifdef __SUNPRO_CC
SunPro C++
#endif
EOF
if $CXX -E conftest.cc | grep SunPro > /dev/null ; then
  # SUNWspro CC, defines __SUNPRO_CC
  cxxcmd=CC
  for word in $CXX; do
    case "$word" in
      CC | */CC) cxxcmd=$word ;;
    esac
  done
  case "$cxxcmd" in
    */*) ;;
    *)
      IFS="${IFS=	 }"; save_ifs="$IFS"; IFS="${IFS}:"
      for dir in $PATH; do
        test -z "$dir" && dir=.
        if test -f "$dir/$cxxcmd"; then
          cxxcmd="$dir/$cxxcmd"
          break
        fi
      done
      IFS="$save_ifs"
      ;;
  esac
  case "$cxxcmd" in
    */bin/CC)
      cxxdir=`echo "$cxxcmd" | sed -e 's,/bin/CC$,,'`
      LIBCXX="-L$cxxdir/lib -lCstd -lCrun"
      ;;
  esac
fi
cat > conftest.cc <<EOF
#ifdef __GNUC__
GCC
#endif
EOF
if $CXX -E conftest.cc | grep GCC > /dev/null ; then
  # GNU g++
changequote(,)dnl
  lib1=`g++ --print-file-name libstdc++.a`
  case "$lib1" in
    */*)
      libdir1=`echo "$lib1" | sed -e 's,/[^/]*$,,'`
      LIBCXX="$LIBCXX -L$libdir1"
      ;;
  esac
  LIBCXX="$LIBCXX -lstdc++"
  lib2=`g++ --print-file-name libgcc.a`
  case "$lib2" in
    */*)
      libdir2=`echo "$lib2" | sed -e 's,/[^/]*$,,'`
      LIBCXX="$LIBCXX -L$libdir2"
      ;;
  esac
  LIBCXX="$LIBCXX -lgcc"
  LIBCXX="$LIBCXX -lm"
changequote([, ])dnl
fi
rm -f conftest.cc
AC_SUBST(LIBCXX)

dnl Check for tools needed for formatting the documentation.
ac_aux_dir_abs=`cd $ac_aux_dir && pwd`
AC_PATH_PROG(DVIPS, dvips, $ac_aux_dir_abs/missing dvips)
AC_PATH_PROG(TEXI2PDF, texi2pdf, $ac_aux_dir_abs/missing texi2pdf)
AC_PATH_PROG(PERL, perl, $ac_aux_dir_abs/missing perl)

AC_OUTPUT([Makefile], [
  dnl Fix unesthetic build commands generated by automake.
  for m in Makefile; do
    sed -e "s,\`test -f \\\$< || echo '\\\$(srcdir)/'\`\\\$<,\\\$<," < $m > $m.tmp
    mv $m.tmp $m
  done
  dnl Fix an automake-1.5 bug.
  for m in Makefile; do
    sed -e 's,^#distdir:,distdir:,' < $m > $m.tmp
    mv $m.tmp $m
  done
  dnl Fix an automake-1.5 bug: all info files are erased by "make".
  for m in Makefile; do
    sed -e '/cd \$(srcdir) && rm -f /d' < $m > $m.tmp
    mv $m.tmp $m
  done
])
