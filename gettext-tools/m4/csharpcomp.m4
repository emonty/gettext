# csharpcomp.m4 serial 1 (gettext-0.13.2)
dnl Copyright (C) 2003 Free Software Foundation, Inc.
dnl This file is free software, distributed under the terms of the GNU
dnl General Public License.  As a special exception to the GNU General
dnl Public License, this file may be distributed as part of a program
dnl that contains a configuration script generated by Autoconf, under
dnl the same distribution terms as the rest of that program.

# Prerequisites of csharpcomp.sh.
# Sets HAVE_CSHARPCOMP to nonempty if csharpcomp.sh will work.

AC_DEFUN([gt_CSHARPCOMP],
[
  AC_MSG_CHECKING([for C[#] compiler])
  HAVE_CSHARPCOMP=1
  pushdef([AC_MSG_CHECKING],[:])dnl
  pushdef([AC_CHECKING],[:])dnl
  pushdef([AC_MSG_RESULT],[:])dnl
  AC_CHECK_PROG(HAVE_CSCC_IN_PATH, cscc, yes)
  AC_CHECK_PROG(HAVE_MCS_IN_PATH, mcs, yes)
  AC_CHECK_PROG(HAVE_CSC_IN_PATH, csc, yes)
  popdef([AC_MSG_RESULT])dnl
  popdef([AC_CHECKING])dnl
  popdef([AC_MSG_CHECKING])dnl
  if test -n "$HAVE_CSCC_IN_PATH" \
     && cscc --version >/dev/null 2>/dev/null; then
    HAVE_CSCC=1
    ac_result="cscc"
  else
    if test -n "$HAVE_MCS_IN_PATH" \
       && mcs --version >/dev/null 2>/dev/null; then
      HAVE_MCS=1
      ac_result="mcs"
    else
      if test -n "$HAVE_CSC_IN_PATH" \
         && csc -help >/dev/null 2>/dev/null; then
        HAVE_CSC=1
        ac_result="csc"
      else
        HAVE_CSHARPEXEC=
        ac_result="no"
      fi
    fi
  fi
  AC_MSG_RESULT([$ac_result])
  AC_SUBST(HAVE_CSCC)
  AC_SUBST(HAVE_MCS)
  AC_SUBST(HAVE_CSC)
])
